
#Область ОписаниеПеременных
#КонецОбласти

#Область ПрограммныйИнтерфейс

// Функция - Обязательные значения заполнены
//
// Параметры:
//  Номенклатура	 - Массив Из Справочники.Номенклатура - ссылка "Номенклатура"
//  ОбязательныеПоля - Массив Из Строка - Имена реквизитов точно как в справочнике "АлкогольДополнительныеСведения"
// 
// Возвращаемое значение:
//  Структура ИЗ
//		*Заполнены - Булево
//		*Сообщение - Массив ИЗ Строка - информация пользователю, для какой позиции нет данных 
//
Функция ОбязательныеЗначенияЗаполнены(Номенклатура, ОбязательныеПоля = Неопределено) Экспорт

	ПроверяемыеПоля = ?(ОбязательныеПоля = Неопределено, ПроверяемыеПоляПоУмолчанию(), ОбязательныеПоля);
	Запрос 			= Новый Запрос;
	Запрос.Текст	= ТекстЗапросаОбязательныеЗначенияЗаполнены();
	Запрос.УстановитьПараметр("Ссылка", Номенклатура);

	РезультаЗапроса = Запрос.Выполнить();
	
	Если РезультаЗапроса.Пустой() Тогда
		Заполнены 	= Ложь;
		Заголовок 	= "Алкоголь доп. сведения отсутствуют!";		
		Ответ 		= НовыйОтветПроверкиЗаполненияОбязательныхЗначений(Заполнены, Заголовок);
		
		ИтераторА = 0;
		Для Каждого э Из Номенклатура Цикл
			Если ИтераторА > 3 Тогда
				// слишком много не надо, так легче ориентироваться
				Прервать;
			КонецЕсли;
			
			Ответ.Сообщение.Добавить(Строка(э));
			ИтераторА = ИтераторА + 1;
		КонецЦикла;		
		
		Возврат Ответ;
		
	КонецЕсли;
	
	Выборка = РезультаЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
	
		Для Каждого Поле Из ПроверяемыеПоля Цикл
			Если Поле = "ГодУрожая" И НЕ ГодУрожаяЧетырехЗначный(Выборка[Поле]) Тогда
				
				Заполнены 	= Ложь;
				Заголовок 	= "Не корректно заполнен ГодУрожая для ";												
				Ответ 		= НовыйОтветПроверкиЗаполненияОбязательныхЗначений(Заполнены, Заголовок);
				
				Ответ.Сообщение.Добавить(Выборка.НоменклатураСсылка);
				
				Возврат Ответ;
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Выборка[Поле]) Тогда

				Заполнены 	= Ложь;
				Заголовок 	= "(алк. доп. сведения) заполните поле """ + Строка(Поле) + """ для:";				
				Ответ 		= НовыйОтветПроверкиЗаполненияОбязательныхЗначений(Заполнены, Заголовок);
				
				Ответ.Сообщение.Добавить(Выборка.НоменклатураСсылка);
				
				Возврат Ответ;
				
			КонецЕсли;
		КонецЦикла;
	
	КонецЦикла;

	Заполнены 			= Истина;
	Заголовок 			= "Проверка заполнения алк. доп. сведения прошла успешно!";
	ОтветПоУмолчанию 	= НовыйОтветПроверкиЗаполненияОбязательныхЗначений(Заполнены, Заголовок);
	
	Возврат ОтветПоУмолчанию;

КонецФункции // ОбязательныеЗначенияЗаполнены()

#КонецОбласти

#Область ОбработчикиСобытий
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ГодУрожаяЧетырехЗначный(ГодУрожая) Экспорт

	ГодУрожаяКакСтрока = Строка(Формат(ГодУрожая, "ЧГ=0"));
	
	Если СтрДлина(ГодУрожаяКакСтрока) <> 4 Тогда
		Сообщить("Год урожая должен состоять из четырех символов!");
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Функция - Проверяемые поля по умолчанию
// 
// Возвращаемое значение:
//  ПроверяемыеПоля - Массив Из Строка - Имена реквизитов точно как в справочнике "АлкогольДополнительныеСведения"
//
Функция ПроверяемыеПоляПоУмолчанию()

	Реквизиты = Метаданные.Справочники.АлкогольДополнительныеСведения.Реквизиты;
	ПроверяемыеПоля = Новый Массив;
	Для каждого Реквизит Из Реквизиты Цикл	
		ПроверяемыеПоля.Добавить(Реквизит.Имя);	
	КонецЦикла;
	
	Возврат ПроверяемыеПоля;
	
КонецФункции

Функция ТекстЗапросаОбязательныеЗначенияЗаполнены()

	Текст =
	"ВЫБРАТЬ
	|	Владелец КАК НоменклатураСсылка,
	|	*
	|ИЗ
	|	Справочник.АлкогольДополнительныеСведения КАК АлкогольДополнительныеСведения
	|ГДЕ
	|	АлкогольДополнительныеСведения.Ссылка.Владелец В(&Ссылка)";
	
	Возврат Текст;
	
КонецФункции // ТекстЗапросаОбязательныеЗначенияЗаполнены()

// Функция - Новый ответ проверки заполнения обязательных значений
//
// Параметры:
//  Заполнены	 - Булево - Истина если все обязательные реквизиты алк. доп. свед. ЗАПОЛНЕНЫ
//  Заголовок	 - Строка - Текст как первый элемент массива свойства "Сообщение" у возвращаемого обк
//  СписокЗнч	 - Массив ИЗ элементов которые могут быть преобразованы в строку 
// 
// Возвращаемое значение:
//  Ответ - Структура:
//		*Заполнены - Булево - Истина если все обязательные реквизиты алк. доп. свед. ЗАПОЛНЕНЫ
//		*Сообщение - Массив ИЗ строка (предпологается хранить наименование номенклатур знч которых не заполнено)
//
Функция НовыйОтветПроверкиЗаполненияОбязательныхЗначений(Заполнены, Заголовок = Неопределено)

	Ответ = Новый Структура;
	
	Ответ.Вставить("Заполнены", Заполнены);
	Ответ.Вставить("Сообщение", Новый Массив);
	
	Если Заголовок <> Неопределено Тогда
		Ответ.Сообщение.Добавить(Заголовок);
	КонецЕсли;

	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область Инициализация
#КонецОбласти